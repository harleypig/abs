# Maintainer:  Alan Young <archlinux@harleypig.com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

# This was a minimal installation, but it has turned into a custom
# installation for my own purposes.
#
# This pkgbuild is based on both the 2.2.x version available in the extra
# repository and the 2.4.x version available in the aur.  Also, the BLFS
# instructions and discussion pages were helpful.
#
# URLs: https://httpd.apache.org/docs/2.4/install.html
# http://www.linuxfromscratch.org/blfs/view/svn/server/apache.html
# https://aur.archlinux.org/packages/ap/apache24/PKGBUILD

pkgname=apache
pkgver=2.4.7
pkgrel=2
pkgdesc='A high performance Unix-based HTTP server'
arch=('i686' 'x86_64')
url='http://www.apache.org/dist/httpd'
license=('APACHE')

# XXX: Check which files need to be preserved across upgrades.

backup=(etc/conf.d/apache
        etc/httpd/conf/extra/httpd-{autoindex,dav,default,info,languages}.conf
        etc/httpd/conf/extra/httpd-{manual,mpm,multilang-errordoc}.conf
        etc/httpd/conf/extra/httpd-{ssl,userdir,vhosts}.conf
        etc/httpd/conf/extra/proxy-html.conf
        etc/httpd/conf/httpd.conf
        etc/logrotate.d/httpd)

depends=('openssl' 'zlib' 'apr-util' 'pcre' 'systemd')
optdepends=('lynx: apachectl status'
            'db: Berkeley DB'
            'doxygen: Documentation')

provides=('apache')
conflicts=('apache')

install=apache.install

source=(http://www.apache.org/dist/httpd/httpd-${pkgver}.tar.bz2
        http://www.apache.org/dist/httpd/httpd-${pkgver}.tar.bz2.asc
        apache.conf.d
        apachectl-confd.patch
        apache.tmpfiles.conf
        arch.layout
        httpd-info.patch
        httpd.logrotate
        httpd.service)

# Generate this with updpkgsums.
md5sums=('170d7fb6fe5f28b87d1878020a9ab94e'
         'SKIP'
         '08b3c875f6260644f2f52b4056d656b0'
         '5581f3e14eec926ca23f07277d0dfa48'
         '26a5d2b1d9e89b339b8608c5002f8c00'
         'dd69fd0c02f7f6e2b923f0c20383a96a'
         '09e2fa6aacd484e464e41b46f6677400'
         '13dbaaf949c5bc36cfcf5718b95cb020'
         'a823bb355c136fd0e2b3fb820e2d903c')

build() {

  cd "${srcdir}/httpd-${pkgver}"

  # Patch various files
  patch -Np0 -i "${srcdir}/apachectl-confd.patch"
  patch -Np0 -i "${srcdir}/httpd-info.patch"

  # Change default user and group.
  sed -e 's#User daemon#User http#'   \
      -e 's#Group daemon#Group http#' \
      -i docs/conf/httpd.conf.in

  # Force the apxs utility to use absolute pathnames for modules, when
  # instructed to do so.
  sed '/dir.*CFG_PREFIX/s@^@#@' -i support/apxs.in

  # Add arch linux directory layout option to config options.
  cat "${srcdir}/arch.layout" >> config.layout

  autoconf

  # See https://httpd.apache.org/docs/2.4/programs/configure.html for more
  # configure options.

  # Use arch linux directory layout.
  config_options="${config_options} --enable-layout=Arch"

  # Build modules as dynamic shared objects (DSO).
  # Let's see what 'few' gets us ... not enough, so let's see what 'most' gets
  # us ...
  config_options="${config_options} --enable-mods-shared=most"

  # Select all MPMs which support dynamic loading on the current platform and
  # build them as DSO modules.
  config_options="${config_options} --enable-mpms-shared=all"

  # See the various bits of documentation for suexec.
  # caller needs to be the same as the default user we set above.
  # docroot needs to match the default docroot we set in layout.
  # safepath is the path suexec can access; since the default resolves to
  #   /usr/bin for all paths, just remove the duplication.
  config_options="${config_options} --enable-suexec=shared"
  config_options="${config_options} --with-suexec-caller=http"
  config_options="${config_options} --with-suexec-docroot=/srv/http"
  config_options="${config_options} --with-suexec-safepath=/usr/bin"

  # We want to use the separate apr and apr-util packages.
  config_options="${config_options} --with-apr=/usr/bin/apr-1-config"
  config_options="${config_options} --with-apr-util=/usr/bin/apu-1-config"

  ./configure $config_options
  make

}

package() {

  cd "${srcdir}/httpd-${pkgver}"
  make DESTDIR="${pkgdir}/" install

  install -D -m644 "${srcdir}/apache.conf.d"        "${pkgdir}/etc/conf.d/apache"
  install -D -m644 "${srcdir}/apache.tmpfiles.conf" "${pkgdir}/usr/lib/tmpfiles.d/apache.conf"
  install -D -m644 "${srcdir}/httpd.logrotate"      "${pkgdir}/etc/logrotate.d/httpd"

  install -m755 -d "${pkgdir}/usr/lib/systemd/system"
  install -m644    "${srcdir}/httpd.service" "${pkgdir}/usr/lib/systemd/system/"

  # symlinks for /etc/httpd
  ln -fs /run/httpd "${pkgdir}/etc/httpd/run"
  ln -fs /usr/lib/httpd/build "${pkgdir}/etc/httpd/build"
  ln -fs /usr/lib/httpd/modules "${pkgdir}/etc/httpd/modules"
  ln -fs /var/log/httpd "${pkgdir}/etc/httpd/logs"

  # Include extra conf files and make sure needed modules are loaded.
  sed -e 's#/usr/lib/httpd/modules/#modules/#' \
      -e 's|#\(Include conf/extra/httpd-autoindex.conf\)|\1|' \
      -e 's|#\(Include conf/extra/httpd-default.conf\)|\1|' \
      -e 's|#\(Include conf/extra/httpd-info.conf\)|\1|' \
      -e 's|#\(Include conf/extra/httpd-languages.conf\)|\1|' \
      -e 's|#\(Include conf/extra/httpd-manual.conf\)|\1|' \
      -e 's|#\(Include conf/extra/httpd-mpm.conf\)|\1|' \
      -e 's|#\(Include conf/extra/httpd-multilang-errordoc.conf\)|\1|' \
      -e 's|#\(Include conf/extra/httpd-userdir.conf\)|\1|' \
      -e 's|#\(Include conf/extra/httpd-vhosts.conf\)|\1|' \
      -e 's|#\(LoadModule include_module \)|\1|' \
      -e 's|#\(LoadModule info_module \)|\1|' \
      -e 's|#\(LoadModule negotiation_module \)|\1|' \
      -e 's|#\(LoadModule request_module \)|\1|' \
      -e 's|#\(LoadModule slotmem_shm_module \)|\1|' \
      -e 's|#\(LoadModule suexec_module \)|\1|' \
      -e 's|#\(LoadModule userdir_module \)|\1|' \
      -i "${pkgdir}/etc/httpd/conf/httpd.conf"

  # cleanup
  rm -rf "${pkgdir}/etc/httpd/conf/original"
  rm -rf "${pkgdir}/srv/"
  rm -rf "${pkgdir}/run"

}
